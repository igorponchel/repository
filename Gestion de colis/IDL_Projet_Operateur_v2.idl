module OperateurDeTransportObjet {

	struct CoordBancaire {
	
		long numeroCarte;
		string proprietaire;
		string date;
		long numDos;
	};
	
	struct CoordBancairePro {
	
		long codeBanque;
		long codeGuichet;
		long numCompte;
		long cleRIB;
	};

	struct Adresse {
		
		string numeroRue;
		string nomRue;
		string ville;
		long departement;
		string pays;
	};
	
	interface GestionUtilisateurs {
	
		struct DemandeInscriptionTrans {
	
			string nomTransporteur;
			Adresse adresseTransporteur;
			CoordBancairePro coordonneesBancairesPro;
		};
	
		struct DemandeAdhesion {
			
			string prenomAdherent;
			string nomAdherent;
			Adresse adresseAdherent;
			CoordBancaire coordonneesBancaires;
		};
	
		struct Adhesion {
		
			long numeroAdherent;
			string motDePasse;
			string nomAdherent;
			string prenomAdherent;
			long zoneAdherent;
		};
		
		struct InscriptionTrans {
		
			long numeroInscritTrans;
			string nomTransporteur;
		};
	
		exception AdherentExistantException {
			string explication;
		};
		exception TransExistantException {
			string explication;
		};
		exception AdherentInexistantException {
			string explication;
		};
		exception TransInexistantException {
			string explication;
		};
		
		Adhesion demandeAdhesion(in DemandeAdhesion demandeAdhesion) raises (AdherentExistantException);
		InscriptionTrans demandeInscriptionTrans(in DemandeInscriptionTrans demandeInscriptionTrans) raises (TransExistantException);
		boolean verifierAdherent(in long numeroAdherent, in string motDePasse); //On suppose que le GestionnaireUtilisateur contient les IOR des adhérents
		boolean verifierTransporteur(in short codeVerif); //On suppose que le GestionnaireUtilisateur contient les IOR des transporteurs
		long getNumAdherent(in string nomAdherent, in string prenomAdherent) raises(AdherentInexistantException);
		long getZoneAdherent(in string nomAdherent, in string prenomAdherent) raises(AdherentInexistantException);
	};
	
	interface GestionnairePaiement {
	
		short debiter(in CoordBancaire coordonneesBancaires, in long somme); // une seule suffit quelque soit l'entité
		short crediter(in CoordBancairePro coordonneesBancairesPro, in long somme);
	};
	
	interface Station {
		
		enum EtatCasier {vide, occupe};
		
		struct Casier {
		
			long numeroCasier;
			EtatCasier etatCasier;
		};
		
		exception AucunCasierDisponibleException {
			string explication;
		};
	

		long reserverCasier(in string idObjet) raises (AucunCasierDisponibleException);
		//signale à la station distante le couple codeTransport/CasierDistant pour assurer la recherche lors de la livraison
		oneway void notifierCodeTransport(in long numeroCasier, in string codeTransport);
	};
	
	interface GestionnaireTransportObjet {
	
		enum EtatObjet {depose, enTransit, livre, delivre}; // delivré signifie que le destinataire l'a récupéré.
	
		struct Objet {
		
			string idObjet;
			EtatObjet etatObjet;
			long numeroCasierDepart;
			long numeroCasierArrivee;
		};
		
		exception ObjetInexistantException {
			string explication;
		};
		//retourne le codeTransport à la station
		string notifierOffreTransport(in string nomStationDepart, in string nomStationArrivee);
		string notifierOffreAcceptee(in long numeroTransporteur, in string numeroOffre);
		boolean verifierTransporteur(in long numeroTransporteur, in string codeTransport);
		void notifierEtatObjet(in string idObjet, in EtatObjet etatObjet) raises (ObjetInexistantException);
		EtatObjet consulterEtatObjet(in string idObjet) raises (ObjetInexistantException);
	};
	
	interface Adherent {
	
		oneway void notifierColisArrive(in string idObjet);
	};
	
	interface Transporteur {
	
		oneway void notifierOffreTransport(in string nomStationDepart, in string nomStationArrivee, in string numeroOffre);
		oneway void notifierOffrePriseEnCharge(in string numeroOffre);
	};
};